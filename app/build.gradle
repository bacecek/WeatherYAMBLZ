apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../jacoco.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.compile_sdk_version
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.zino.mobilization.weatheryamblz"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    lintOptions {
        lintConfig file('lint.xml')
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            buildConfigField 'com.zino.mobilization.weatheryamblz.utils.StethoHelper', 'STETHO', 'new com.zino.mobilization.weatheryamblz.utils.DebugStethoHelper()'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'com.zino.mobilization.weatheryamblz.utils.StethoHelper', 'STETHO', 'new com.zino.mobilization.weatheryamblz.utils.ReleaseStethoHelper()'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$rootProject.espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$rootProject.espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    })
    compile "com.android.support:appcompat-v7:$rootProject.support_version"
    compile "com.android.support:design:$rootProject.support_version"
    compile "com.android.support:support-v4:$rootProject.support_version"
    compile "com.android.support:cardview-v7:$rootProject.support_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_version"

    compile "com.jakewharton:butterknife:$rootProject.butter_knife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butter_knife_version"

    compile "com.arello-mobile:moxy:$rootProject.moxy_version"
    compile "com.arello-mobile:moxy-app-compat:$rootProject.moxy_version"
    annotationProcessor "com.arello-mobile:moxy-compiler:$rootProject.moxy_version"

    testCompile "junit:junit:$rootProject.junit_version"
    testCompile "org.hamcrest:hamcrest-library:$rootProject.hamcrest_version"
    testCompile "org.mockito:mockito-core:$rootProject.mockito_version"
    androidTestCompile "org.mockito:mockito-android:$rootProject.mockito_version"
    testCompile "org.robolectric:robolectric:$rootProject.robolectric_version"
    testAnnotationProcessor "com.arello-mobile:moxy-compiler:$rootProject.moxy_version"

    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit_version"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.rxjava_version"
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroid_version"
    compile "com.f2prateek.rx.preferences2:rx-preferences:$rootProject.rxprefs_version"

    compile "com.evernote:android-job:$rootProject.androidjob_version"

    debugCompile "com.facebook.stetho:stetho:$rootProject.stetho_version"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho_version"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leak_canary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leak_canary_version"

    compile "com.google.dagger:dagger:$rootProject.dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger_version"

    compile "com.jakewharton.timber:timber:$rootProject.timber_version"
    compile "com.jakewharton.rxbinding2:rxbinding:$rootProject.rxbinding_version"

    compile "android.arch.persistence.room:runtime:$rootProject.room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.room_version"
    compile "android.arch.persistence.room:rxjava2:$rootProject.room_version"
}
